plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode") ?: "1"
def flutterVersionName = localProperties.getProperty("flutter.versionName") ?: "1.0"

android {
    namespace = "com.mineinasx.app"
    compileSdk = 35 
    
    // FIX: Forçando a versão exata que o EnX OS usa na Workflow
    ndkVersion "25.2.9519653"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    signingConfigs {
        release {
            if (keystoreProperties.containsKey('storeFile')) {
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
                storePassword = keystoreProperties['storePassword']
                storeFile = file(keystoreProperties['storeFile'])
            }
        }
    }

    defaultConfig {
        applicationId = "com.mineinasx.app"
        minSdk = 21 
        targetSdk = 35
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -O3"
                abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            signingConfig = keystoreProperties.containsKey('storeFile') ? signingConfigs.release : signingConfigs.debug
            
            minifyEnabled false
            shrinkResources false
        }
    }
}

flutter {
    source = "../.."
}
